plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}

Properties debugSigning = new Properties()
debugSigning.load(new FileInputStream(file(project.property("debug-signing.properties"))))

Properties releaseSigning = new Properties()
releaseSigning.load(new FileInputStream(file(project.property("release-signing.properties"))))

android {
    compileSdk 32
    defaultConfig {
        applicationId "cn.neday.graduates"
        minSdk 21
        targetSdk 32
        versionCode 180
        versionName "1.8.0"
        resConfigs "zh"
        ndk {
            // 设置支持的SO库架构
            abiFilters 'armeabi', 'x86', 'armeabi-v7a', 'x86_64', 'arm64-v8a'
        }
    }
    signingConfigs {
        debug {
            storeFile file(debugSigning['storeFile'])
            storePassword debugSigning['storePassword']
            keyAlias debugSigning['keyAlias']
            keyPassword debugSigning['keyPassword']
        }
        release {
            storeFile file(releaseSigning['storeFile'])
            storePassword releaseSigning['storePassword']
            keyAlias releaseSigning['keyAlias']
            keyPassword releaseSigning['keyPassword']
        }
    }
    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            signingConfig signingConfigs.debug
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar", "*.aar"])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.7.10"

    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'com.nineoldandroids:library:2.4.0'

    // Calligraphy3
    implementation 'io.github.inflationx:calligraphy3:3.1.1'
    implementation 'io.github.inflationx:viewpump:2.0.3'

    // Key-Value Storage
    implementation 'com.tencent:mmkv:1.2.13'
    // Utils
    implementation 'com.blankj:utilcodex:1.31.0'
    // Android Debug Database
    debugImplementation 'com.amitshekhar.android:debug-db:1.0.6'

    implementation 'cn.leancloud:realtime-android:8.2.10'
    implementation 'cn.leancloud:storage-android:8.2.10'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
}
